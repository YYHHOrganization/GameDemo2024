#pragma kernel CSMain
static const int threadGroupSize = 1024;

struct Boid
{
    float3 position;
    float3 direction;

    float3 flockHeading;
    float3 flockCentre;
    float3 separationHeading;
    int numFlockmates;//群体中的成员数量
};

RWStructuredBuffer<Boid> boids;
int numBoids;
float viewRadius;
float avoidRadius;

[numthreads(threadGroupSize,1,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    for (int indexB = 0; indexB < numBoids; indexB ++)
    {
        if (id.x != indexB)
        {
            Boid boidB = boids[indexB];
            float3 offset = boidB.position - boids[id.x].position;
            float sqrDst = offset.x * offset.x + offset.y * offset.y + offset.z * offset.z;

            //如果在视野范围内
            if (sqrDst < viewRadius * viewRadius)
            {
                boids[id.x].numFlockmates += 1;
                //计算群体的平均方向和位置
                boids[id.x].flockHeading += boidB.direction;
                boids[id.x].flockCentre += boidB.position;

                //如果在避障范围内
                if (sqrDst < avoidRadius * avoidRadius)
                {
                    //计算避障方向
                    boids[id.x].separationHeading -= offset / sqrDst;
                }
            }
        }
    }
}